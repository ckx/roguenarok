function	script	SetStartingStats	{
	statusup2 getarg(0),19; // Main stat
	statusup2 getarg(1),9; // Sub stat
	return;
}

function	script	ChangeJob	{
	// ensure max basic skill
	if (getskilllv("NV_BASIC") < 9) {
		dispbottom "wut";
		skill "NV_BASIC",9,SKILL_PERM_GRANT;
	}

	// everyone gets free first aid, maybe we should put this in Main tab and just not drop a scroll for it?
	skill "NV_FIRSTAID",1,SKILL_PERM_GRANT;

	.@jobToBe = getarg(0);
	// actually change the job
	jobchange .@jobToBe;

	switch (Class) {
		//Define starting stuff based on class 
		case Job_Swordman:
			SetStartingStats(bStr, bDex);
			setarray .@startingMainSkillId$[0],"SM_BASH","SM_ENDURE","SM_PROVOKE";
			setarray .@startingMainSkillLv[0],3,3,3;
			setarray .@startingEtcSkillId$[0],"SM_SWORD","KN_SPEARMASTERY","SM_TWOHAND","SM_RECOVERY","SM_MOVINGRECOVERY","SM_FATALBLOW";
			setarray .@startingEtcSkillLv[0],10,10,10,10,1,1;
			setarray .@startingItems[0],1101,2106,2315,2506,2406;
			break;
		case Job_Mage:
			SetStartingStats(bInt, bDex);
			setarray .@startingMainSkillId$[0],"MG_FROSTDIVER","MG_FIREBOLT","MG_LIGHTNINGBOLT","MG_SOULSTRIKE","MG_SIGHT";
			setarray .@startingMainSkillLv[0],3,3,3,2,1;
			setarray .@startingEtcSkillId$[0],"MG_SRECOVERY","MG_ENERGYCOAT";
			setarray .@startingEtcSkillLv[0],10,1;
			setarray .@startingItems[0],1601,2102,2302,2502,2402;
			break;
		case Job_Archer:
			SetStartingStats(bDex, bAgi);
			setarray .@startingMainSkillId$[0],"AC_DOUBLE";
			setarray .@startingMainSkillLv[0],10;
			setarray .@startingEtcSkillId$[0],"AC_OWL","AC_VULTURE","AC_CONCENTRATION";
			setarray .@startingEtcSkillLv[0],10,10,5;
			setarray .@startingItems[0],1701,2331,2502,2404,1750;
			break;
		case Job_Acolyte:
			SetStartingStats(bDex, bVit);
			setarray .@startingMainSkillId$[0],"AL_HEAL","AL_BLESSING","AL_INCAGI","AL_PNEUMA","AL_ANGELUS","AL_RUWACH","AL_HOLYLIGHT";
			setarray .@startingMainSkillLv[0],1,5,5,1,3,1,1;
			setarray .@startingEtcSkillId$[0],"PR_MACEMASTERY","MG_SRECOVERY";
			setarray .@startingEtcSkillLv[0],10,10;
			setarray .@startingItems[0],1504,2102,2322,2502,2404;
			break;
		case Job_Thief:
			SetStartingStats(bAgi, bDex);
			setarray .@startingMainSkillId$[0],"TF_HIDING","TF_STEAL","TF_BACKSLIDING","TF_DOUBLE";
			setarray .@startingMainSkillLv[0],1,1,1,1;
			setarray .@startingEtcSkillId$[0],"RGX_DAGGERMASTERY","TF_MISS";
			setarray .@startingEtcSkillLv[0],10,10;
			setarray .@startingItems[0],1201,2304,2502,2404;
			break;
		case Job_Merchant:
			SetStartingStats(bDex, bStr);
			setarray .@startingMainSkillId$[0],"MC_MAMMONITE";
			setarray .@startingMainSkillLv[0],10;
			setarray .@startingEtcSkillId$[0],"AM_AXEMASTERY","MC_INCCARRY","MC_DISCOUNT","MC_PUSHCART","BS_UNFAIRLYTRICK","MC_CHANGECART","MC_CARTREVOLUTION";
			setarray .@startingEtcSkillLv[0],10,10,10,10,1,1,1;
			setarray .@startingItems[0],1301,2104,2313,2506,2406;
			break;
		default: 
			mes "I dunno what job that is. Sorry. Gimme pets instead?";
			close;
			break;
	}

	// Give actives (perm skills)
	for (.@i = 0; .@i < getarraysize(.@startingMainSkillId$); .@i++) {
		skill .@startingMainSkillId$[.@i],.@startingMainSkillLv[.@i],SKILL_PERM;
	}

	// Give passives (grant skills)
	for (.@i = 0; .@i < getarraysize(.@startingEtcSkillId$); .@i++) {
		skill .@startingEtcSkillId$[.@i],.@startingEtcSkillLv[.@i],SKILL_PERM_GRANT;
	}

	// Give & equip items
	for (.@i = 0; .@i < getarraysize(.@startingItems); .@i++) {
		getitembound .@startingItems[.@i],1,Bound_Char;
		equip .@startingItems[.@i];
	}

	//everyone gets 100 status points
	StatusPoint += 100;

	//everyone except archer/merch get 9 skill points (their actives just come maxed)
	if (Class != Job_Archer && Class != Job_Merchant) { 
		SkillPoint += 9;
	}

	//merchants get a cart & cart item
	if (Class == Job_Merchant) {
		setcart;
		getitembound 800007,40,Bound_Char; // Antique Junk coin
		//todo: cart whistle
	}
	close;
}

function	script	Transcend	{
	switch (Class) {
		case Job_Knight:
			jobchange 4008; //lk
			setarray .@transcMainSkillId$[0],"LK_CONCENTRATION","LK_AURABLADE","LK_BERSERK";
			setarray .@transcMainSkillLv[0],1,1,1;
			setarray .@transcEtcSkillId$[0],"RGX_ADVSWORDMASTERY","RGX_ADV2HSMASTERY","RGX_ADVSPEARMASTERY","LK_TENSIONRELAX";
			setarray .@transcEtcSkillLv[0],5,5,5,1;
			break;
	}

	.@numberOfMainSkills = getarraysize(.@transcMainSkillId$);
	.@numberOfEtcSkills = getarraysize(.@transcEtcSkillId$);

	// Give actives (perm skills)
	if (.@numberOfMainSkills > 0) {
		for (.@i = 0; .@i < .@numberOfMainSkills; .@i++) {
			skill .@transcMainSkillId$[.@i],.@transcMainSkillLv[.@i],SKILL_PERM;
		}
	}
	// Give passives (grant skills)
	if (.@numberOfEtcSkills > 0) {
		for (.@i = 0; .@i < .@numberOfEtcSkills; .@i++) {
			skill .@transcEtcSkillId$[.@i],.@transcEtcSkillLv[.@i],SKILL_PERM_GRANT;
		}
	}


	JobLevel += 49;
	SkillPoint -= 49;
}

function	script	SecondJobChange	{
	// ensure max basic skill
	if (getskilllv("NV_BASIC") < 9) {
		skill "NV_BASIC",9,SKILL_PERM;
	}

	jobchange getarg(0);
	//todo: armor masteries
	switch (Class) {
		case Job_Knight:
			setarray .@secondMainSkillId$[0],"KN_CHARGEATK";
			setarray .@secondMainSkillLv[0],1;
			setarray .@secondEtcSkillId$[0],"RGX_ADVSWORDMASTERY","RGX_ADV2HSMASTERY","RGX_ADVSPEARMASTERY","KN_RIDING","KN_CAVALIERMASTERY";
			setarray .@secondEtcSkillLv[0],1,1,1,1,5;
			//todo: give peco whistle
			break;
		case Job_Crusader:
			setarray .@secondMainSkillId$[0],"CR_AUTOGUARD","CR_SHRINK","CR_SHIELDBOOMERANG","CR_PROVIDENCE";
			setarray .@secondMainSkillLv[0],1,1,1,3;
			setarray .@secondEtcSkillId$[0],"CR_TRUST","CR_DEVOTION","KN_RIDING","KN_CAVALIERMASTERY";
			setarray .@secondEtcSkillLv[0],10,5,1,5;
			//todo: give peco whistle
			break;
		case Job_Wizard:
			SkillPoint += 5;
			setarray .@secondMainSkillId$[0],"WZ_STORMGUST","WZ_VERMILION","WZ_JUPITEL","WZ_EARTHSPIKE","WZ_QUAGMIRE","WZ_SIGHTBLASTER";
			setarray .@secondMainSkillLv[0],1,1,5,1,1,1;
			break;
		case Job_Sage:
			setarray .@secondMainSkillId$[0],"SA_FREECAST","SA_SPELLBREAKER","SA_AUTOSPELL","SA_FLAMELAUNCHER","SA_FROSTWEAPON","SA_LIGHTNINGLOADER","SA_SEISMICWEAPON";
			setarray .@secondMainSkillLv[0],1,1,2,5,5,5,5;
			setarray .@secondEtcSkillId$[0],"SA_ADVANCEDBOOK";
			setarray .@secondEtcSkillLv[0],10;
			getitem 715,30; // yellow gems
			getitem 716,30; // red gems
			getitem 717,30; // blue gems
			break;
		case Job_Hunter:
			setarray .@secondMainSkillId$[0],"HT_LANDMINE","HT_SANDMAN","HT_REMOVETRAP";
			setarray .@secondMainSkillLv[0],1,1,1;
			setarray .@secondEtcSkillId$[0],"RGX_BOWMASTERY","HT_FALCON","HT_STEELCROW","HT_DETECTING","HT_BLITZBEAT","HT_SPRINGTRAP";
			setarray .@secondEtcSkillLv[0],10,1,10,4,5,5;
			getitem 1065,50;
			//todo: falcon whistle
			break;
		case Job_Bard:
		case Job_Dancer:
			setarray .@secondMainSkillId$[0],"BD_ENCORE","BA_MUSICALSTRIKE","DC_THROWARROW","BA_DISSONANCE","DC_SERVICEFORYOU","BD_LULLABY";
			setarray .@secondMainSkillLv[0],1,5,5,1,1,1;
			setarray .@secondEtcSkillId$[0],"BA_MUSICALLESSON","DC_DANCINGLESSON","BD_ADAPTATION";
			setarray .@secondEtcSkillLv[0],10,10,1;
			//todo: combine the bard/dancer job bonus charts
			break;
		case Job_Priest:
			setarray .@secondMainSkillId$[0],"PR_IMPOSITIO","PR_SUFFRAGIUM","PR_STRECOVERY";
			setarray .@secondMainSkillLv[0],1,1,1;
			setarray .@secondEtcSkillId$[0],"HP_MANARECHARGE","PR_KYRIE","ALL_RESURRECTION";
			setarray .@secondEtcSkillLv[0],3,10,5;
			getitem 7285,1; //holy thread (res catalyst)
			break;
		case Job_Monk:
			setarray .@secondMainSkillId$[0],"MO_CALLSPIRITS","MO_ABSORBSPIRITS","MO_EXPLOSIONSPIRITS","MO_TRIPLEATTACK","MO_CHAINCOMBO","MO_INVESTIGATE";
			setarray .@secondMainSkillLv[0],5,1,5,3,1,1;
			setarray .@secondEtcSkillId$[0],"MO_IRONHAND","MO_DODGE","MO_SPIRITSRECOVERY";
			setarray .@secondEtcSkillLv[0],10,10,5;
			//todo: buff spirit recovery
			break;
		case Job_Assassin:
			setarray .@secondMainSkillId$[0],"AS_ENCHANTPOISON","AS_POISONREACT";
			setarray .@secondMainSkillLv[0],1,10;
			setarray .@secondEtcSkillId$[0],"AS_KATAR","AS_CLOAKING";
			setarray .@secondEtcSkillLv[0],10,10;
			getitem 2280,1; // naruto
			equip 2280;
			break;
		case Job_Rogue:
			setarray .@secondMainSkillId$[0],"RG_BACKSTAP";
			setarray .@secondMainSkillLv[0],1;
			setarray .@secondEtcSkillId$[0],"RGX_BOWMASTERY","SM_SWORD","AS_RIGHT","AS_LEFT","RG_SNATCHER","RG_TUNNELDRIVE","RG_PLAGIARISM";
			setarray .@secondEtcSkillLv[0],10,10,5,3,5,5,5;
			break;
		case Job_Blacksmith:
			setarray .@secondMainSkillId$[0],"BS_ADRENALINE","BS_MAXIMIZE","BS_REPAIRWEAPON","BS_HAMMERFALL";
			setarray .@secondMainSkillLv[0],3,1,1,3;
			setarray .@secondEtcSkillId$[0],"RGX_ADVAXEMASTERY","BS_WEAPONRESEARCH","BS_SKINTEMPER","ALL_INCCARRY";
			setarray .@secondEtcSkillLv[0],1,10,5,5;
			getitem 14511,3;
			break;
		case Job_Alchemist:
			//todo: custom chem protect armor passive
			//todo: spirit ball pitcher
			setarray .@secondMainSkillId$[0],"AM_POTIONPITCHER","AM_CANNIBALIZE","AM_ACIDTERROR";
			setarray .@secondMainSkillLv[0],5,1,1;
			setarray .@secondEtcSkillId$[0],"AM_LEARNINGPOTION","ALL_INCCARRY";
			setarray .@secondEtcSkillLv[0],10,5;
			getitem 547,100; // slim whites
			getitem 505,50; // blues
			getitem 7136,100; // acid bottles
			getitem 7135,50; // bottle grenades
			getitem 7137,50; // plant bottles
			getitem 7138,100; // marine spheres
			break;	
	}

	.@numberOfMainSkills = getarraysize(.@secondMainSkillId$);
	.@numberOfEtcSkills = getarraysize(.@secondEtcSkillId$);

	// Give actives (perm skills)
	if (.@numberOfMainSkills > 0) {
		for (.@i = 0; .@i < .@numberOfMainSkills; .@i++) {
			skill .@secondMainSkillId$[.@i],.@secondMainSkillLv[.@i],SKILL_PERM;
		}
	}
	// Give passives (grant skills)
	if (.@numberOfEtcSkills > 0) {
		for (.@i = 0; .@i < .@numberOfEtcSkills; .@i++) {
			skill .@secondEtcSkillId$[.@i],.@secondEtcSkillLv[.@i],SKILL_PERM_GRANT;
		}
	}

	// give out the rentals
	if (Class == Job_Knight || Class == Job_Crusader) { 
		setriding;
	}

	if (Class == Job_Hunter) {
		setfalcon;
	}
	return;
}
