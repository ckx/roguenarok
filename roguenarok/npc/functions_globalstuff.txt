function	script	Neuralyzer	{
	//remove rentals
	if (checkriding()) setriding 0;
	if (checkfalcon()) setfalcon 0;
	if (checkcart()) setcart 0;
	//get/remove all skills
	getskilllist(getcharid(0));
	for (.@i = 0; .@i < @skilllist_count; .@i++ ) {
		// don't reset basic skill
		if (@skilllist_id[.@i] == 1) {
			continue;
		}
		dispbottom "Resetting skill ID: " + @skilllist_id[.@i] + " was level: " + @skilllist_lv[.@i];
		skill @skilllist_id[.@i],0,0;
	}
	//remove all skill/stat/zeny points
	SkillPoint = 0;
	StatusPoint = 0;
	Zeny = 0;

	resetlvl 1;
	resetstatus;

	return;
}

function	script	GiveRentals	{
	//todo: all the whistles
	switch (Class) {
		case Job_Merchant:
			setcart;
			break;
		case Job_Knight:
		case Job_Crusader:
			setriding;
			break;
		case Job_Hunter:
			setfalcon;
			break;
		default:
			return;
	}
	return;
}

function	script	DeleteInventory	{
	getinventorylist();
	for (.@i = 0; .@i < @inventorylist_count; ++.@i) {
		delitemidx @inventorylist_idx[.@i];
	}
	return;
}

function	script	BecomeGhost	{
	.@charid = getcharid(0);
	//todo: make the player sprite have a bio mob effect instead of just using bio mobs
	//todo: finish this, add novice mob The Last One (2441)
	//todo: lock all gears, etc (maybe do this on the map side?)
	switch (Class) {
		case Job_Novice:
		case Job_Swordman:
			disguise 1652,.@charid;
			break;
		case Job_Mage:
			disguise 1657,.@charid;
			break;
		case Job_Acolyte:
			disguise 1655,.@charid;
			break;
		case Job_Archer:
			disguise 1656,.@charid;
			break;
		case Job_Thief:
			disguise 1653,.@charid;
			break;
		case Job_Merchant:
			disguise 1654,.@charid;
			break;
		default:
			disguise 1651,.@charid;
			break;
	}
	return;
}

function	script	RemoveGhost	{
	@isGhost = false;
	undisguise;	
	jobchange Job_Novice;
	DeleteInventory();
	return;
}