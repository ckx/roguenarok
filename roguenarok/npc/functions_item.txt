function	script	ClothArmorMastery	{
	.@maxHpBonus = 80;
	.@maxSpBonus = 100;
	.@spRecovBonus = 20;
	.@castSpdBonus = 15;

	.@secondJobMultiplier = 2;
	.@transcMultiplier = 3;

	switch (Class) {
		case Job_Mage:
		case Job_Acolyte:
			bonus bMaxHP,.@maxHpBonus;
			bonus bMaxSP,.@maxSpBonus;
			bonus bSPrecovRate,.@spRecovBonus;
			bonus bCastrate,-.@castSpdBonus;
			break;
		case Job_Wizard:
		case Job_Sage:
		case Job_Priest:
		case Job_Monk:
			bonus bMaxHP,.@maxHpBonus * .@secondJobMultiplier;
			bonus bMaxSP,.@maxSpBonus * .@secondJobMultiplier;
			bonus bSPrecovRate,.@spRecovBonus * .@secondJobMultiplier;
			bonus bCastrate,-.@castSpdBonus * .@secondJobMultiplier;
			break;
		default:
			return;
	}
}

function	script	LightArmorMastery	{
	.@maxHpBonus = 100;
	.@fleeBonus = 10;
	.@aspdBonus = 2;
	.@critBonus = 3;

	.@secondJobMultiplier = 2;
	.@transcMultiplier = 3;

	switch (Class) {
		case Job_Thief:
		case Job_Archer:
			bonus bMaxHP,.@maxHpBonus;
			bonus bFlee,.@fleeBonus;
			bonus bAspd,.@aspdBonus;
			bonus bCritical,.@critBonus;
			break;
		case Job_Assassin:
		case Job_Rogue:
		case Job_Hunter:
		case Job_Bard:
		case Job_Dancer:
			bonus bMaxHP,.@maxHpBonus * .@secondJobMultiplier;
			bonus bFlee,.@fleeBonus * .@secondJobMultiplier;
			bonus bAspd,.@aspdBonus * .@secondJobMultiplier;
			bonus bCritical,.@critBonus * .@secondJobMultiplier;
			break;
		default:
			return;
	}
}

function	script	HeavyArmorMastery	{
	.@maxHpBonus = 120;
	.@hpRecovBonus = 20;
	.@defRateBonus = 10;
	.@defBonus = 10; // doesnt get multiplied

	.@secondJobMultiplier = 2;
	.@transcMultiplier = 3;

	switch (Class) {
		case Job_Swordman:
		case Job_Merchant:
			bonus bMaxHP,.@maxHpBonus;
			bonus bHPrecovRate,.@hpRecovBonus;
			bonus bDefRate,.@defRateBonus;
			bonus bDef,.@defBonus;
			break;
		case Job_Knight:
		case Job_Crusader:
		case Job_Blacksmith:
		case Job_Alchemist:
			bonus bMaxHP,.@maxHpBonus * .@secondJobMultiplier;
			bonus bHPrecovRate,.@hpRecovBonus * .@secondJobMultiplier;
			bonus bDefRate,.@defRateBonus * .@secondJobMultiplier;
			bonus bDef,.@defBonus;
			break;
		default: 
			return;
	}
}

function	script	AddNamedSkill	{
	.@skillname$ = getarg(0);
	.@lv = getskilllv(.@skillname$);
	.@itemname$ = getarg(0) + "_SC";

	if (.@lv > 0) {
		dispbottom "You already know " + .@skillname$ + ".";
		return;
	}

	if (SkillPoint >= 1) {
        skill .@skillname$,1,SKILL_PERM;
        SkillPoint -= 1;
		delitem .@itemname$,1;
		return;
      } else { 
        dispbottom "You require more skill points.";
		return;
      }
}

function	script	PermAddNamedSkill	{
	.@skillname$ = getarg(0);
	.@lv = getskilllv(.@skillname$);
	.@itemname$ = getarg(0) + "_SC";

	if (.@lv > 0) {
		dispbottom "You already know " + .@skillname$ + ".";
		return;
	}

	if (SkillPoint >= 1) {
        skill .@skillname$,1,SKILL_PERM_GRANT;
        SkillPoint -= 1;
		delitem .@itemname$,1;
		return;
      } else { 
        dispbottom "You require more skill points.";
		return;
      }
}

function	script	RemoveNamedSkill	{
	.@skillname$ = getarg(0);
	.@lv = getskilllv(.@skillname$);
	.@itemname$ = getarg(0) + "_REM";

	if (.@lv < 1) {
		dispbottom "You don't have any points in " + .@skillname$;
		return;
	}

	resetnamedskill(.@skillname$);
	delitem .@itemname$,1;
	dispbottom "Reset " + .@skillname$ + ", refunding " + .@lv + " skill point(s).";
	return;
}

function	script	SecondJobSelect	{
	
	.@arg1$ = jobname(getarg(0));
	.@arg2$ = jobname(getarg(1));
	//parse gender for Bard/Dancer
	if(getarg(1) == Job_Bard && Sex == SEX_FEMALE) {
			.@arg2$ = jobname(Job_Dancer);
	}
	switch(select(.@arg1$,.@arg2$)) {
		case 1: 
			SecondJobChange(getarg(0));
			delitem jobname(BaseClass)+"_CHANGER",1;
			end;
		case 2: 
			SecondJobChange(getarg(1));
			delitem jobname(BaseClass)+"_CHANGER",1;
			end;
	}
	end;
}