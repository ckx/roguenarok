function	script	AddNamedSkill	{
	.@skillname$ = getarg(0);
	.@lv = getskilllv(.@skillname$);
	.@itemname$ = getarg(0) + "_SC";

	if (.@lv > 0) {
		dispbottom "You already know " + .@skillname$ + ".";
		return;
	}

	if (SkillPoint >= 1) {
        skill .@skillname$,1,SKILL_PERM_GRANT;
        SkillPoint -= 1;
		delitem .@itemname$,1;
		return;
      } else { 
        dispbottom "You require more skill points.";
		return;
      }
}

function	script	RemoveNamedSkill	{
	.@skillname$ = getarg(0);
	.@lv = getskilllv(.@skillname$);
	.@itemname$ = getarg(0) + "_REM";

	if (.@lv < 1) {
		dispbottom "You don't have any points in " + .@skillname$;
		return;
	}

	resetnamedskill(.@skillname$);
	delitem .@itemname$,1;
	dispbottom "Reset " + .@skillname$ + ", refunding " + .@lv + " skill point(s).";
	return;
}

function	script	ClothArmorMastery	{
	if (Class == 4010) { // Only work on High Wizard
		bonus bCastrate,-60;
	}
}

function	script	HeavyArmorMastery	{
	switch (Class) {
		case Job_Swordman:
		case Job_Merchant:
			bonus bMaxHP,100;
			bonus bHPrecovRate,20;
			bonus bDef,20;
			break;
		default: 
			return;
	}
}

function	script	Neuralyzer	{
	//remove rentals
	if (checkriding()) setriding 0;
	if (checkfalcon()) setfalcon 0;
	if (checkcart()) setcart 0;
	//get/remove all skills
	getskilllist(getcharid(0));
	for (.@i = 0; .@i < @skilllist_count; .@i++ ) {
		// don't reset basic skill
		if (@skilllist_id[.@i] == 1) {
			continue;
		}
		dispbottom "Resetting skill ID: " + @skilllist_id[.@i] + " was level: " + @skilllist_lv[.@i];
		skill @skilllist_id[.@i],0,0;
	}
	//remove all skill points
	SkillPoint = 0;
	StatusPoint = 0;
	return;
}

function	script	SecondJobSelect	{
	
	.@arg1$ = jobname(getarg(0));
	.@arg2$ = jobname(getarg(1));
	//parse gender for Bard/Dancer
	if(getarg(1) == Job_Bard && Sex == SEX_FEMALE) {
			.@arg2$ = jobname(Job_Dancer);
	}
	switch(select(.@arg1$,.@arg2$)) {
		case 1: 
			SecondJobChange(getarg(0));
			delitem jobname(BaseClass)+"_CHANGER",1;
			end;
		case 2: 
			SecondJobChange(getarg(1));
			delitem jobname(BaseClass)+"_CHANGER",1;
			end;
	}
	end;
}