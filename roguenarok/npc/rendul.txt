function	script	SetStartingStats	{
	statusup2 getarg(0),19; // Main stat
	statusup2 getarg(1),9; // Sub stat
	return;
}

function	script	ChangeJob	{
	.@jobToBe = getarg(0);
	// actually change the job
	jobchange .@jobToBe;

	switch (Class) {
		//Define starting stuff based on class 
		case Job_Swordman:
			SetStartingStats(bStr, bDex);
			setarray .@startingMainSkillId$[0],"SM_BASH","SM_ENDURE","SM_PROVOKE";
			setarray .@startingMainSkillLv[0],3,3,3;
			setarray .@startingEtcSkillId$[0],"SM_SWORD","KN_SPEARMASTERY","SM_TWOHAND","SM_RECOVERY","SM_MOVINGRECOVERY","SM_FATALBLOW";
			setarray .@startingEtcSkillLv[0],10,10,10,10,1,1;
			setarray .@startingItems[0],1101,2106,2315,2506,2406;
			break;
		case Job_Mage:
			SetStartingStats(bInt, bDex);
			setarray .@startingMainSkillId$[0],"MG_FROSTDIVER","MG_FIREBOLT","MG_LIGHTNINGBOLT","MG_SOULSTRIKE","MG_SIGHT";
			setarray .@startingMainSkillLv[0],3,3,3,2,1;
			setarray .@startingEtcSkillId$[0],"MG_SRECOVERY","MG_ENERGYCOAT";
			setarray .@startingEtcSkillLv[0],10,1;
			setarray .@startingItems[0],1601,2102,2302,2502,2402;
			break;
		case Job_Archer:
			SetStartingStats(bDex, bAgi);
			setarray .@startingMainSkillId$[0],"AC_DOUBLE";
			setarray .@startingMainSkillLv[0],10;
			setarray .@startingEtcSkillId$[0],"AC_OWL","AC_VULTURE","AC_CONCENTRATION";
			setarray .@startingEtcSkillLv[0],10,10,5;
			setarray .@startingItems[0],1701,2331,2502,2404,1750;
			break;
		case Job_Acolyte:
			SetStartingStats(bDex, bVit);
			setarray .@startingMainSkillId$[0],"AL_HEAL","AL_BLESSING","AL_INCAGI","AL_PNEUMA","AL_ANGELUS","AL_RUWACH","AL_HOLYLIGHT";
			setarray .@startingMainSkillLv[0],1,5,5,1,3,1,1;
			setarray .@startingEtcSkillId$[0],"PR_MACEMASTERY","MG_SRECOVERY";
			setarray .@startingEtcSkillLv[0],10,10;
			setarray .@startingItems[0],1504,2102,2322,2502,2404;
			break;
		case Job_Thief:
			SetStartingStats(bAgi, bDex);
			setarray .@startingMainSkillId$[0],"TF_HIDING","TF_STEAL","TF_BACKSLIDING","TF_DOUBLE";
			setarray .@startingMainSkillLv[0],1,1,1,1;
			setarray .@startingEtcSkillId$[0],"RGX_DAGGERMASTERY","TF_MISS";
			setarray .@startingEtcSkillLv[0],10,10;
			setarray .@startingItems[0],1201,2304,2502,2404;
			break;
		case Job_Merchant:
			SetStartingStats(bDex, bStr);
			setarray .@startingMainSkillId$[0],"MC_MAMMONITE";
			setarray .@startingMainSkillLv[0],10;
			setarray .@startingEtcSkillId$[0],"AM_AXEMASTERY","MC_INCCARRY","MC_DISCOUNT","MC_PUSHCART","BS_UNFAIRLYTRICK","MC_CHANGECART","MC_CARTREVOLUTION";
			setarray .@startingEtcSkillLv[0],10,10,10,10,1,1,1;
			setarray .@startingItems[0],1301,2104,2313,2506,2406;
			getitembound 673,500,Bound_Char;
			break;
		default: 
			mes "I dunno what job that is. Sorry. Gimme pets instead?";
			close;
			break;
	}

	// Give actives (perm skills)
	for (.@i = 0; .@i < getarraysize(.@startingMainSkillId$); .@i++) {
		skill .@startingMainSkillId$[.@i],.@startingMainSkillLv[.@i],SKILL_PERM;
	}

	// Give passives (grant skills)
	for (.@i = 0; .@i < getarraysize(.@startingEtcSkillId$); .@i++) {
		skill .@startingEtcSkillId$[.@i],.@startingEtcSkillLv[.@i],SKILL_PERM_GRANT;
	}

	// Give & equip items
	for (.@i = 0; .@i < getarraysize(.@startingItems); .@i++) {
		getitembound .@startingItems[.@i],1,Bound_Char;
		equip .@startingItems[.@i];
	}

	//everyone gets 100 status points
	StatusPoint += 100;

	//everyone except archer/merch get 9 skill points (their actives just come maxed)
	if (Class != Job_Archer && Class != Job_Merchant) { 
		SkillPoint += 9;
	}

	//merchants get a cart & cart item
	if (Class == Job_Merchant) {
		setcart;
		//todo: cart whistle
	}
	close;
}

hugel,90,150,6	script	Rendul	495,{
	if	(Class == 0) {
		// TODO: message picked from random pool of messages.
		mes .NPCName$;
		mes "Ah... Another lost Einherjar hoping to break free, nya? Your soul longs for peace, fervid warrior. Perhaps you should put these foolish ambitions to rest, nya?";
		menu "Swordman:Mage:Archer:Acolyte:Thief:Merchant:Pet",-;
			if (@menu == 1) {
				.@jobSelection = Job_Swordman;
			} else if (@menu == 2) {
				.@jobSelection = Job_Mage;
			} else if (@menu == 3) { 
				.@jobSelection = Job_Archer;
			} else if (@menu == 4) {
				.@jobSelection = Job_Acolyte;
			} else if (@menu == 5) {
				.@jobSelection = Job_Thief;
			} else if (@menu == 6) { 
				.@jobSelection = Job_Merchant;
			} else if (@menu == 7) {
				next;
				mes "*Rendul quietly purrs as you scratch underneath his chin.*";
				close;
			}
	} else {
		mes "Nya? What's this? So you've managed to reclaim some life? What a rare occurence.";
		mes "..."; 
		mes "... Is it everything you hoped for, norman?";
		close;
	}

	ChangeJob(.@jobSelection);

	OnInit:
		.NPCName$ = "Rendul";
}

